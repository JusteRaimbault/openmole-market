


to ant:return-to-nest  ;; ant procedure
  ifelse nest?
  [ ;; drop food and head out again
    set color red
    rt 180 ]
  [ set chemical chemical + 60  ;; drop some chemical
    ant:uphill-nest-scent ]         ;; head toward the greatest value of nest-scent
end

to ant:look-for-food  ;; ant procedure
  if food > 0 [
    ;; pick up food
    set color orange + 1     
    ;; reduce the food source
    if not infinite-food? [set food food - 1]
    rt 180                   ;; and turn around
    stop ]
  ;; go in the direction where the chemical smell is strongest
  if (chemical >= 0.05) and (chemical < 2)[
    ant:uphill-chemical
  ]
end

;; sniff left and right, and go where the strongest smell is
to ant:uphill-chemical  ;; ant procedure
  let scent-ahead ant:chemical-scent-at-angle   0
  let scent-right ant:chemical-scent-at-angle  45
  let scent-left  ant:chemical-scent-at-angle -45
  if (scent-right > scent-ahead) or (scent-left > scent-ahead)
  [ ifelse scent-right > scent-left
    [ rt 45 ]
    [ lt 45 ] ]
end

;; sniff left and right, and go where the strongest smell is
to ant:uphill-nest-scent  ;; ant procedure
  let scent-ahead ant:nest-scent-at-angle   0
  let scent-right ant:nest-scent-at-angle  45
  let scent-left  ant:nest-scent-at-angle -45
  if (scent-right > scent-ahead) or (scent-left > scent-ahead)[
     ifelse scent-right > scent-left[
       rt 45
     ][
       lt 45
     ]
  ]
end

to ant:wiggle  ;; ant procedure
  rt random wiggle-angle
  lt random wiggle-angle
  if not can-move? 1 [ rt 180 ]
end

to-report ant:nest-scent-at-angle [angle]
  let p patch-right-and-ahead angle 1
  if p = nobody [ report 0 ]
  report [nest-scent] of p
end

to-report ant:chemical-scent-at-angle [angle]
  let p patch-right-and-ahead angle 1
  if p = nobody [ report 0 ]
  report [chemical] of p
end



;;
; ant constructor
to ant:new-ant
    set size 2      ;; easier to see
    set color red   ;; red = not carrying food
    set carried-food 0
end
